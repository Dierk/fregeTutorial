module suggest.LineProducer where

{--
    Type for a consumer in state thread 's'
    that builds up an accumulation of type 'a'
    when consuming a product of type 'p'
-}
type Consumer     s a p = a → p → ST s a

---  Consumer type for products of type String
type LineConsumer s a   = Consumer s a String

{--
    @LineProducer@ is a class for all types that support a common interface for in-place
    line-by-line processing that might result in immediate effects (like writing to
    output) or building up a data structure based on the line processing.

    This approach only makes sense for data that is read from external sources or is otherwise
    unpredictable (e.g. random).

    Notable examples are @BufferedReader@, @File@, @URL@, and @stdin@, while
    some line producers are not even IO dependent like @StringReader@ and @StringGenerator@.
-}
class LineProducer lp where
    {-- The _onLine_ function is called to process a line whenever a line is produced.

        > onLine lineProducer startValue consume

        where _consume_ is a @LineConsumer@ function that takes an accumulator of the same
        type as the _startValue_ and the newly produced line as a @String@.
        It returns the accumulated value as the result of a
        state transition (most often that is IO).

        The _onLine_ function returns the total accumulated value as the result of a state transition.
    -}
    onLine  :: Mutable s lp → a → LineConsumer s a → ST s a

    {--
        The _offLine_ function stops the production (and therefore also consumption) of further lines.

        It is usually called from the @LineConsumer@ to end processing before all lines are processed.
        In that case, it is *not guaranteed* that any trailing instructions after calling _offLine_ in
        the LineConsumer are executed.
    -}
    offLine :: Mutable s lp → ST s ()

instance LineProducer BufferedReader where
    onLine :: Mutable s BufferedReader → a → LineConsumer s a → ST s a
    onLine bufferedReader accu consume = do
        line <- bufferedReader.readLine
            `catch` \(e :: IOException)  -> return Nothing
        case line of
            Nothing   -> do
                offLine bufferedReader
                return accu
            Just line -> do
                newValue <- consume accu line
                onLine bufferedReader newValue consume      -- recurse
    offLine :: Mutable s BufferedReader → ST s ()
    offLine bufferedReader = bufferedReader.close           -- throws IOE for read after close

data StringReader = native java.io.StringReader where
    native new      :: String -> STMutable s StringReader
    native close    :: Mutable s StringReader -> ST s ()

instance LineProducer StringReader where
    onLine  :: Mutable s StringReader → a → LineConsumer s a → ST s a
    onLine  stringReader accu consume = (BufferedReader.new stringReader) >>= \br -> onLine br accu consume
    offLine :: Mutable s StringReader → ST s ()
    offLine stringReader = stringReader.close

data FileInputStream = native java.io.FileInputStream where
    native new :: String  -> STMutable s FileInputStream        -- todo: needs to be changed in frege
                    throws FileNotFoundException

openReader :: String -> STMutable s BufferedReader              -- todo: needs to be changed in frege
openReader fileName = do
    fis <- FileInputStream.new (fileName :: String )
    isr <- InputStreamReader.new fis "UTF-8"
    BufferedReader.new isr

data LpFile = native java.io.File where
    native new           :: String -> STMutable s LpFile
    native getPath       :: Mutable s LpFile -> ST s String

-- convenience function, not really needed
makeMutable :: File -> STMutable s LpFile
makeMutable file = LpFile.new file.getPath

{- Convenience functions for line producers that delegate to a buffered reader and
   signal the end of processing via throwing EOF, which we catch and properly
   close the reader.
   Used for File and URL.
-}
private relayWithCatch bufferedReader accu consume =
    onLine bufferedReader accu $ \currentState line -> do
        consume currentState line `catch` \(e :: EOFException) -> do
            offLine bufferedReader
            return currentState
private relayOffLine _ = do
    throwST (EOFException.new "end of lines")
    return ()

instance LineProducer LpFile where
    onLine  :: Mutable s LpFile → a → LineConsumer s a → ST s a
    onLine  file accu consume = do
        path <- file.getPath
        bufferedReader <- openReader path -- todo allow more encodings than UTF-8
        relayWithCatch bufferedReader accu consume
    offLine :: Mutable s LpFile → ST s ()
    offLine = relayOffLine

data MalformedURLException = pure native java.net.MalformedURLException  -- not needed after integration
derive Exceptional MalformedURLException

data LpURL = native java.net.URL where
    native new              :: String -> STMutable s LpURL                throws MalformedURLException
    native openStream       :: Mutable s LpURL -> STMutable s InputStream throws IOException

instance LineProducer LpURL where
    onLine  :: Mutable s LpURL → a → LineConsumer s a → ST s a
    onLine  xurl accu consume = do
        uis <- LpURL.openStream xurl
        isr <- InputStreamReader.new uis "UTF-8"    -- todo: encoding depends on the protocol ...
        bufferedReader <- BufferedReader.new isr
        relayWithCatch bufferedReader accu consume
    offLine :: Mutable s LpURL → ST s ()
    offLine = relayOffLine

countStringReaderLines :: ST s Int      -- just to show that IO is not always needed
countStringReaderLines = do
    stringReader <- StringReader.new $ unlines ["first","second","third"]
    result <- stringReader.onLine 0 $ \num _ -> return  $ num + 1
    return result

data StrGenData = StrGen {
    proceed     :: Bool,                -- if false we stop generating lines
    line        :: String,              -- the currently generated line, "onLine" will be called with this
    nextLine    :: String -> String     -- the generation function for the next line
}
type StringGenerator = Ref StrGenData   -- a mutable reference to the generator data

instance LineProducer StringGenerator where
    onLine  :: Mutable s StringGenerator → a → LineConsumer s a → ST s a
    onLine  strGen accu consume = do
        state <- strGen.get
        if state.proceed
        then do
            newValue <- consume accu state.line        -- call handler with the last generated line (might call offLine)
            strGen.modify _.{line <- state.nextLine}    -- generate the next line
            onLine strGen newValue consume              -- continue processing with updated values
        else
            return accu
    offLine :: Mutable s StringGenerator → ST s ()
    offLine strGen = strGen.modify _.{proceed = false}  -- called by handler to stop generating lines

main = do
    testFile = File.new "TestFile.txt"              -- since version 3.25, otherwise <- instead of =

    println "----------------------------------------"
    println "general file handling"
    -- delete test file if it already existed
    result <- testFile.delete
    println $ "Test file deleted to clean up before start: " ++ show result

    println "create test file"
    writeFile testFile.getPath $ unlines ["first line","second line","third line"]
    result <- testFile.exists
    println $ "File now exists: " ++ show result

    println "read test file in toto"
    content <- readFile testFile.getPath
    println "file content was:"
    println content

    println "append 2 lines"
    appendFile testFile.getPath $ unlines ["fourth line","fifth line"]

    println "----------------------------------------"
    println "processing each line with a buffered reader, while keeping track of line numbers"
    bufferedReader <- openReader testFile.getPath
    count <- bufferedReader.onLine 0 $ \num line -> do
        println $ show (num + 1) ++ ": " ++ line
        return  $ num + 1
    println $ "total number of lines: " ++ show count

    println "----------------------------------------"
    println "processing each line from a file, pushing each line on a stack"
    mutFile <- makeMutable testFile
    stack   <- mutFile.onLine [] $ \stack line -> return (line : stack)
    println $ "total stack" ++ show stack

    println "----------------------------------------"
    println "reading only one line (a header for example)"
    oneLineReader <- openReader testFile.getPath
    header <- oneLineReader.onLine "" $ \state line -> do
        offLine oneLineReader -- make sure the next read yields no more lines
        return line
    println $ "the header line is: " ++ show header

    println "----------------------------------------"
    println "processing each line with a non-IO impure reader, here: StringReader. (great for testing)"
    numLines <- countStringReaderLines
    println $ "processing strings with StringReader works as expected: " ++ show (3 == numLines)

    println "----------------------------------------"
    println "reading from a URL"
    urlReader <- LpURL.new "http://google.com"
    result <- urlReader.onLine 0 $ \num line -> do
        println $ show num ++ ": " ++ line.substr 0 10
        if (num > 3) then urlReader.offLine else return ()
        return  $ num + 1
    println $ "processing strings from URL works as expected: " ++ show (result == 4)

    println "----------------------------------------"
    println "reading from a StringGenerator"
    strGenRef <- Ref.new $ StrGen { proceed = true, line = "", nextLine = (++ "x") }
    result <- strGenRef.onLine 0 $ \num line ->
        if (num > 3)
        then do
            strGenRef.offLine
            return num
        else do
            println $ show num ++ ": " ++ line
            return  $ num + 1
    println $ "StringGenerator works as expected: " ++ show (result == 4)

cli = do
    println "command line input (max 10 lines or send EOF via Cmd/Ctrl-D)"
    stdin.onLine 0 $ \num line -> do
        println $ show (num + 1) ++ ": " ++ line
        if (num > 9) then stdin.offLine else return ()
        return  $ num + 1
    return ()
